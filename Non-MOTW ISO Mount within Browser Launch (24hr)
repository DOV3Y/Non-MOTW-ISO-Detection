//Credits to Reprise99 for original 20 minute version. 
//As there was no MOTW-ISO Detetion prior to the patch, this KQL detects moutning of ISO files after browsers have been opened i.e mostly from phishing emails.
//Mounting the ISO File > Launching the URL - Although due to mount times I find 1d/24h to be much better.

//M365 Defender Tables

//Microsoft Sentinel query
//Find events where an ISO file is mounted by detecting the creation of a .iso.lnk file
DeviceFileEvents
| where TimeGenerated > ago(1d)
| where ActionType == "FileCreated"
//There will be false positives from legitmately mounted files here, but it's good for trackcing the lnk containers the ISO malware operates from
| where FileName endswith "iso.lnk"
| project
    ISOMountTime=TimeGenerated,
    DeviceName,
    FileName,
    FolderPath,
    InitiatingProcessAccountName
//DeviceEvents openeing URL
| join kind=inner(
    DeviceEvents
    | where TimeGenerated > ago(1d)
    | where ActionType == "BrowserLaunchedToOpenUrl"
    //Find only RemoteURLs that are web sites
    | where RemoteUrl startswith "http"
    | project
        URLOpenTime=TimeGenerated,
        DeviceName,
        InitiatingProcessAccountName,
        RemoteIP,
        RemoteUrl,
        RemotePort
    )
    on DeviceName, InitiatingProcessAccountName
//Browser to Mount within a timeframe (customizable) - I find 24 hours (1d) is best due to potential mount time.
| where URLOpenTime between ((ISOMountTime - timespan(0min)) .. (ISOMountTime + timespan(1d)))
| extend ['ISO FileName'] = trim(@".lnk", FileName)
| project
    ISOMountTime,
    URLOpenTime,
    ['ISO FileName'],
    DeviceName,
    InitiatingProcessAccountName,
    RemoteUrl,
    RemoteIP,
    RemotePort


//Advanced Hunting License Required

DeviceFileEvents
| where Timestamp > ago(1d)
| where ActionType == "FileCreated"
//This may create some false positives with files named iso.lnk at the end like summary-ciso.lnk
| where FileName endswith "iso.lnk"
| project
    ISOMountTime=Timestamp,
    DeviceName,
    FileName,
    FolderPath,
    InitiatingProcessAccountName
//Combine device events
| join kind=inner(
    DeviceEvents
    | where Timestamp > ago(1d)
    | where ActionType == "BrowserLaunchedToOpenUrl"
    //Remote URLS
    | where RemoteUrl startswith "http"
    | project
        URLOpenTime=Timestamp,
        DeviceName,
        InitiatingProcessAccountName,
        RemoteIP,
        RemoteUrl,
        RemotePort
    )
    on DeviceName, InitiatingProcessAccountName
//Find browser opened to URL events that happened within 1d  of the ISO file being mounted
| where URLOpenTime between ((ISOMountTime - timespan(0min)) .. (ISOMountTime + timespan(1d)))
| extend ['ISO FileName'] = trim(@".lnk", FileName)
| project
    ISOMountTime,
    URLOpenTime,
    ['ISO FileName'],
    DeviceName,
    InitiatingProcessAccountName,
    RemoteUrl,
    RemoteIP,
    RemotePort
